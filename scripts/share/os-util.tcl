package provide OsUtil 1.0
package require CommonUtil
package require YamlUtil

namespace eval ::OsUtil {
  variable chars {abcdefghijklmnopqrstuvwzyzABC:;'<>?,./DEFGHIJKLMNOPQRSTUVWXYZ1234567890~!@#$%^&*()_+{}[]|}
}

proc ::OsUtil::createUserNotLogin {un} {
  if {[catch {exec useradd -M -s /bin/false $un} msg o]} {
    puts $msg
  }
}

proc ::OsUtil::getHostName {} {
  set c [exec hostnamectl status]
  if {[regexp {Static hostname:\s*(\S+)} $c mh m1]} {
    return $m1
  } else {
    return {}
  }
}

proc ::OsUtil::openFirewall {prot args} {
  foreach port $args {
    exec firewall-cmd --permanent --zone=public --add-port ${port}/$prot
  }
  exec firewall-cmd --reload
}

proc ::OsUtil::randPass {size} {
  variable chars
  set pchars [list]
  set len [string length $chars]
  for {set x 0} {$x < $size} {incr x} {
   set idx [expr {int(rand()*$len)}]
   lappend pchars [string index $chars $idx]
  }
  return [join $pchars {}]
}

proc ::OsUtil::doCommonTasks {ymlDict rawParamDict} {
  set ns [::YamlUtil::findValue $ymlDict NameServer $rawParamDict]
  if {[string length $ns] > 0} {
    disableNetworkManager
    changeResolver $ns
  }
  set hn [::YamlUtil::findValue $ymlDict HostName $rawParamDict]
  if {[string length $hn] > 0} {
    exec hostnamectl set-hostname $hn --static
  }
}

proc ::OsUtil::changeResolver {resolver} {
  if {! [file exists /etc/resolv.conf.origin]} {
    exec cp /etc/resolv.conf /etc/resolv.conf.origin
  }
  set fid [open /etc/resolv.conf w]
  puts $fid "# Generated by EasyInstaller"
  puts $fid "nameserver $resolver"
  close $fid
}

proc ::OsUtil::disableNetworkManager {} {
  catch {
    exec systemctl stop NetworkManager
    exec systemctl disable NetworkManager
  } msg o
}
