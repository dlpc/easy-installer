package provide OsUtil 1.0
package require CommonUtil
package require YamlUtil
package require Expect

namespace eval ::OsUtil {
  variable chars {abcdefghijklmnopqrstuvwzyzABC:;'<>?,./DEFGHIJKLMNOPQRSTUVWXYZ1234567890~!@#$%^&*()_+{}[]|}
}

proc ::OsUtil::setAlternative {name realPathToMatch} {
  spawn alternatives --config $name
  expect {
    "or type selection number: $" {
      set lines [split $expect_out(buffer) \n]
      foreach line $lines {
        if {[regexp [format {\s+(\d+)\s+(.+%s)$} $name] [string trimright $line] mh m1 m2]} {
          if {[string match *$realPathToMatch* $m2]} {
            exp_send "$m1\n"
            exp_continue
          }
        }
      }
    }
    eof {
      puts done
    }
    timeout {
      puts timeout
    }
  }
}

proc ::OsUtil::installNtp {} {
  ::CommonUtil::spawnCommand yum install -y ntp ntpdate
  exec systemctl enable ntpd
  ::CommonUtil::spawnCommand ntpdate pool.ntp.org
  exec systemctl start ntpd
}

proc ::OsUtil::getAppHome {name args} {
  if {[catch {set nameLink [exec which $name]} msg o]} {
    puts $msg
    ::CommonUtil::endEasyInstall
  }
  while {[file type $nameLink] eq {link}} {
    set nameLink [file readlink $nameLink]
  }
  return [file normalize [file join $nameLink {*}$args]]
}

proc ::OsUtil::writeProfiled {profiled lines} {
  if {[catch {open $profiled w} fid o]} {
    puts $fid
    ::CommonUtil::endEasyInstall
  } else {
    foreach line $lines {
      if {[regexp {^(.*)=(.*)$} $line mh mk mv]} {
        set ::env($mk) $mv
      }
      puts $fid $line
    }
    close $fid
  }
}

proc ::OsUtil::createUserNotLogin {un} {
  if {[catch {exec useradd -M -s /bin/false $un} msg o]} {
    puts $msg
  }
}

proc ::OsUtil::getHostName {} {
  set c [exec hostnamectl status]
  if {[regexp {Static hostname:\s*(\S+)} $c mh m1]} {
    return $m1
  } else {
    return {}
  }
}

proc ::OsUtil::openFirewall {prot args} {
  foreach port $args {
    catch {exec firewall-cmd --permanent --zone=public --add-port ${port}/$prot} msg o
  }
  catch {exec firewall-cmd --reload} msg o
}

proc ::OsUtil::randPass {size} {
  variable chars
  set pchars [list]
  set len [string length $chars]
  for {set x 0} {$x < $size} {incr x} {
   set idx [expr {int(rand()*$len)}]
   lappend pchars [string index $chars $idx]
  }
  return [join $pchars {}]
}

proc ::OsUtil::doCommonTasks {ymlDict rawParamDict} {
  set ns [::YamlUtil::findValue $ymlDict NameServer $rawParamDict]
  if {[string length $ns] > 0} {
    disableNetworkManager
    changeResolver $ns
  }
  set hn [::YamlUtil::findValue $ymlDict HostName $rawParamDict]
  if {[string length $hn] > 0} {
    exec hostnamectl set-hostname $hn --static
  }
}

proc ::OsUtil::changeResolver {resolver} {
  if {! [file exists /etc/resolv.conf.origin]} {
    exec cp /etc/resolv.conf /etc/resolv.conf.origin
  }
  set fid [open /etc/resolv.conf w]
  puts $fid "# Generated by EasyInstaller"
  puts $fid "nameserver $resolver"
  close $fid
}

proc ::OsUtil::disableNetworkManager {} {
  catch {
    exec systemctl stop NetworkManager
    exec systemctl disable NetworkManager
  } msg o
}
